#include "raytracing_global.inc"

// Raytracing output to texture, accessed as UAV
RWTexture2D<uint4> shadowMask :register(u0);
RWTexture2D<float4> diffuseResult :register(u1);
RWTexture2D<float4> specularResult :register(u2);

// Constants used to define the camera state
ConstantBuffer<RaytracedSceneConstants> sceneConstants : register(b0, space0);
ConstantBuffer<SceneLight> sceneLight: register(b1, space0);

bool IsInsideViewport(float2 p, float4 viewport)
{
	return (p.x >= viewport.x && p.x <= viewport.z)
		&& (p.y >= viewport.y && p.y <= viewport.w);
}

void ExtractMaterialPropertiesFromGBuffer(
	in float2 screenUV,
	out float roughness,
	out float3 baseReflection,
	out float metalness)
{
	float3 specular = GetBindlessTexture4f(sceneConstants.specularTexureIndex).SampleLevel(pointSampler, screenUV.xy, 0).rgb;
	float3 materialProperties = GetBindlessTexture4f(sceneConstants.materialTextureIndex).SampleLevel(pointSampler, screenUV.xy, 0).rgb;

	ExtractMaterialProperties(
	specular, materialProperties,
	roughness, baseReflection, metalness);
}

float3 GetSpecularResult(in SceneLight light, in float3 lightFalloff, in float3 normal, in float3 pointToEye, in float3 pointToLight, in float specMultiplier)
{
	const half specularPower = 10.0f;
	half hDotN = dot3(normalize(pointToEye + pointToLight), normal);
	half3 specularContribution = _half3(pow( hDotN, specularPower ));

	return specularContribution * (light.color.rgb * specMultiplier) * lightFalloff.rgb;
}

float3 GetValidLightLocation(in SceneLight light, in Plane surfacePlane, in float3 randomOffset)
{
	float3 randVector = normalize(rand3(randomOffset.xyz) * 2.0 - 1.0);

	// Find the farthest we can travel along the vector before we are outsize of the light radius or behind the point
	float3 pointOnPlane = light.center - randVector * (PlaneDotPoint(surfacePlane, light.center) / PlaneDotVector(surfacePlane, randVector)); 
	float maxRad = min(light.emissiveRadius, distance(pointOnPlane, light.center));

	float3 emissiveOffset = randVector * maxRad * rand(randomOffset.zxy);
	float3 lightPosition = light.center + emissiveOffset;

	return lightPosition;
}

void HandleAmbientLight(
	in SceneLight light, 
	in float3 worldPosition,
	in float3 normal,
	in float3 pointToEye,
	out float3 diffuseResult, 
	out float3 specularResult)
{
	// Matching interactionAmbient.pixel shader
	// TODO: Calculate Ambient Occlusion

	const half3 ambientLightVector = half3( 0.5f, 9.5f - 0.385f, 0.8925f );
	half3 lightVector = normalize( ambientLightVector );

	float intensity = saturate(dot(normal, lightVector));
	float3 lightColor = float3(intensity, intensity, intensity);

	if(intensity > 0.0) //TODO: Do this only for point lights and find the true radius distance based on the 3d radius vector and center offset.
	{
		lightColor = lightColor * 
			CalculateFalloff(light, float4(worldPosition, 1.0)) * 
			CalculateProjection(light, float4(worldPosition, 1.0));
	}

	diffuseResult = light.color.rgb * lightColor.rgb;
	specularResult = GetSpecularResult(light, lightColor, normal, pointToEye, lightVector, 2.0 /* Keeping consistant original method */);
}

void HandlePointLight(
	in SceneLight light, 
	in uint lightRayCount, 
	in bool castsShadows, 
	in Plane surfacePlane,
	in float3 normal,
	in float3 pointToEye,
	in float3 worldPosition, 
	in float2 screenUV,
	in float rayStartDistance,
	out float3 diffuseResult, 
	out float3 specularResult)
{
	diffuseResult = float3(0.0, 0.0, 0.0);
	specularResult = float3(0.0, 0.0, 0.0);

	if(isnan(worldPosition.x) || isnan(worldPosition.y) || isnan(worldPosition.z))
	{
		// Invalid world position
		return;
	}

	float3 baseReflection;
	float roughness;
	float metalness;
	ExtractMaterialPropertiesFromGBuffer(screenUV, roughness, baseReflection, metalness);

	float3 pointToCenter = normalize(light.center - worldPosition);

	for(uint rayIndex = 0; rayIndex < lightRayCount; ++rayIndex)
	{
		float3 lightPosition = castsShadows ?
			GetValidLightLocation(
				light, surfacePlane,
				normalize(float3(screenUV.xy + float2(light.sceneIndex, rayIndex) + sceneConstants.noiseOffset, rayIndex + light.sceneIndex))
				)
			: light.center;

		float maxDistance = distance(lightPosition, worldPosition);
		float3 lightVector = normalize(lightPosition - worldPosition);

		if(isnan(lightVector.x) || isnan(lightVector.y) || isnan(lightVector.z))
		{
			// Invalid light position
			continue;
		}

		float3 localDiffuse = float3(0.0, 0.0, 0.0);
		float3 localSpecular = float3(0.0, 0.0, 0.0);
		float intensity = CalcluateLightImpact(
			light,
			worldPosition,
			pointToEye,
			normal,
			lightVector,
			1.0, // Since we divide by our count later, we can just use 1.0 here. Original algorithm calls for PI
			baseReflection,
			roughness,
			localDiffuse,
			localSpecular);

		if (intensity <= 0.0)
		{
			// No light is being cast.
			continue;
		}
		
		if(castsShadows)
		{ 
			RayQuery<RAY_FLAG_CULL_NON_OPAQUE | RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH> inlineQuery;

			// We are inside the light radius.
			// Define a ray, consisting of origin, direction, and the min-max distance values
			RayDesc ray;
			ray.TMin = 0.0001;
			ray.TMax = maxDistance - ray.TMin;
			ray.Origin = worldPosition + (normal * rayStartDistance);
			ray.Direction = lightVector;

			if(ray.TMin >= ray.TMax)
			{
				// Ray is too short to trace.
				continue;
			}

			inlineQuery.TraceRayInline(
				SceneBVH,
				RAY_FLAG_CULL_NON_OPAQUE | RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH, // OR'd with flags above
				INSTANCE_MASK_CAST_SHADOW, // Only cast against shadow casting objects.
				ray);
			inlineQuery.Proceed(); // Run the tracer.
				
			if(inlineQuery.CommittedStatus() == COMMITTED_TRIANGLE_HIT)
			{
				continue; // We hit a shadow so we should not add any data to the colors
			}
		}

		diffuseResult += localDiffuse;
		specularResult += localSpecular;
	}

	diffuseResult = saturate(diffuseResult / lightRayCount); // TODO: Change this to a colour light space
	specularResult = saturate(specularResult / lightRayCount);
}

[shader("raygeneration")]
void RayGen() {	
	// Get the ray location on the screen
	//float2 lerpValues = ((float2)DispatchRaysIndex() + 0.0) / (float2)DispatchRaysDimensions();
	//float3 screenLocation = float3(
	//	lerp(sceneConstants.rp.scissor.x, sceneConstants.rp.scissor.z, lerpValues.x),
	//	lerp(sceneConstants.rp.scissor.y, sceneConstants.rp.scissor.w, 1.0 - lerpValues.y), // We need to flip the y coordinates due to the directx screen coordinates.
	//	0.0f);

	float3 resultDiffuse = float3(0.0, 0.0, 0.0);
	float3 resultSpecular = float3(0.0, 0.0, 0.0);

	// Get the depth buffer location
	float2 screenUV = (((float2)DispatchRaysIndex() + 0.5) + sceneConstants.rp.scissor.xy) / sceneConstants.rp.viewport.zw;
	screenUV.y = 1.0 - screenUV.y;
	
	float3 worldPosition = GetBindlessTexture4f(sceneConstants.positionTextureIndex).SampleLevel(pointSampler, screenUV.xy, 0).xyz;
	float3 flatNormal = normalize((GetBindlessTexture4f(sceneConstants.flatNormalIndex).SampleLevel(pointSampler, screenUV.xy, 0).xyz * 2.0) - 1.0);
	float3 normal = normalize((GetBindlessTexture4f(sceneConstants.normalIndex).SampleLevel(pointSampler, screenUV.xy, 0).xyz * 2.0) - 1.0);

	// Calculate the offset from the ray start we'd like to use.
	// This is done to help with precision errors the further away our surface is.
	float rayStartDistance = 0.001; //StartDistanceFromSurface(worldPosition, sceneConstants.rp.globalEyePos.xyz);
	
	float3 pointToEye = normalize(sceneConstants.rp.globalEyePos.xyz - worldPosition);

	Plane surfacePlane = GeneratePlane(flatNormal, worldPosition);

	//if (IsInsideViewport(screenLocation.xy, sceneLight.scissor))
	//{
		bool castsShadows = (sceneLight.flags & LIGHT_FLAG_CASTS_SHADOWS) > 0;
		uint lightRayCount = castsShadows ? sceneConstants.raysPerLight : 1; // Only use 1 for non shadow surfaces
					
		float3 localDiffuse = float3(0.0, 0.0, 0.0);
		float3 localSpecular = float3(0.0, 0.0, 0.0);

		if((sceneLight.flags & LIGHT_FLAG_AMBIENT_LIGHT) > 0)
		{
			HandleAmbientLight(
				sceneLight, 
				worldPosition, normal, pointToEye,
				resultDiffuse, resultSpecular);
		}
		else if((sceneLight.flags & LIGHT_FLAG_POINT_LIGHT) > 0)
		{
			HandlePointLight(
				sceneLight, lightRayCount, 
				castsShadows, surfacePlane, normal,
				pointToEye, worldPosition, screenUV,
				rayStartDistance, resultDiffuse, resultSpecular);
		}

		if((sceneLight.flags & LIGHT_FLAG_FOG_LIGHT))
		{
			// TODO: Light Fog
			// Should we send this data to a separate texture, or do all fodg lights at once?
		}
	//}

	// Send to the output buffers
	uint2 screenIndex = (uint2)(screenUV * sceneConstants.rp.viewport.zw);
	shadowMask[screenIndex] += uint4(sceneLight.sceneIndex, 0, 3, 255); //TODO: Remove completely from system.
	diffuseResult[screenIndex] += float4(resultDiffuse.rgb, 1.0);
	specularResult[screenIndex] += float4(resultSpecular.rgb, 1.0);
}